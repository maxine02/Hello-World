//===== Hercules Script ======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,142,173,6	script	Coin Shop#1	4_M_MOCASS2,{ callfunc "coinshop"; }


// Script Core
//============================================================
-	script	shop_coin	FAKE_NPC,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Headgears - Up","Headgears - Mid","Headgears - Low","Consumables";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------
set .tier,5000;
set .tier1,500;
set .tier2,1000;
set .tier3,1500;
set .tier4,2000;
set .tier5,2500;
set .tier6,3000;


    //UPPER
	Add(1,5336 ,1,0,0,29200,.tier1); //RGM HAT            
	Add(1,5423 ,1,0,0,29200,.tier5); //ILC                
	Add(1,5138 ,1,0,0,29200,.tier3); //magic eyes         
	Add(1,5388 ,1,0,0,29200,.tier1); //Snake Head Hat     
	Add(1,5297 ,1,0,0,29200,.tier1); //Soul Wing          
	Add(1,5395 ,1,0,0,29200,.tier1); //Striped Hat        
	Add(1,5184 ,1,0,0,29200,.tier1); //Captain's Hat      
	Add(1,5200 ,1,0,0,29200,.tier1); //Coppola            
	Add(1,5490 ,1,0,0,29200,.tier3); //anubis helm        
	Add(1,18613,1,0,0,29200,.tier5); //black musang       
	Add(1,18612,1,0,0,29200,.tier5); //white musang    
	Add(1,5308 ,1,0,0,29200,.tier3); //brazil nat. flag.  
	Add(1,5376 ,1,0,0,29200,.tier5); //FEW                
	Add(1,5481 ,1,0,0,29200,.tier3); //hermode cap        
	Add(1,5379 ,1,0,0,29200,.tier3); //Tam                
	Add(1,5453 ,1,0,0,29200,.tier3); //rwc copper         
	Add(1,5452 ,1,0,0,29200,.tier4); //rwc silver         
	Add(1,5451 ,1,0,0,29200,.tier5); //rwc gold           
	Add(1,5359 ,1,0,0,29200,.tier4); //Ship Captain Hat   
	Add(1,5137 ,1,0,0,29200,.tier4); //alice doll         
	Add(1,5210 ,1,0,0,29200,.tier4); //FAW                
	Add(1,5374 ,1,0,0,29200,.tier5); //GMG                
	Add(1,5495 ,1,0,0,29200,.tier5); //Power of Thor      
	Add(1,18600,1,0,0,29200,.tier5); //Cat Ear Beret      
	Add(1,5420 ,1,0,0,29200,.tier5); //Mask Of Ifrit      
	Add(1,5564 ,1,0,0,29200,.tier5); //Crown of Deceit    
	Add(1,5373 ,1,0,0,29200,.tier3); //Dark Randgris Helm 
	Add(1,5151 ,1,0,0,29200,.tier3); //Note Headphones    
	Add(1,5375 ,1,0,0,29200,.tier3); //Orc Hero Headdress 
	Add(1,5498 ,1,0,0,29200,.tier2); //Wandering Wolf Helm

    //MID
	Add(2,5471,1,0,0,29200,.tier5);//Hairband Of Reginleif
	Add(2,5401,1,0,0,29200,.tier4);//Black Frame Glasses  
	Add(2,5389,1,0,0,29200,.tier4);//angel spirit         
	Add(2,5325,1,0,0,29200,.tier4);//ROBO eye             
	Add(2,5421,1,0,0,29200,.tier4);//Ears Of Ifrit        
	Add(2,5592,1,0,0,29200,.tier4);//Sigrun's Wings       
	Add(2,18518,1,0,0,29200,.tier4);//Angel Wing Ears      
	Add(2,18519,1,0,0,29200,.tier4);//Evil Wing Ears       
	Add(2,5135,1,0,0,29200,.tier1);//Cyclop's Eye         
	Add(2,5397,1,0,0,29200,.tier1);//Scuba Gear     

    //LOW
	Add(3,5305,1,0,0,29200,.tier2);//Pirate Dagger     
	Add(3,5463,1,0,0,29200,.tier2);//Rainbow Scarf     
	Add(3,5361,1,0,0,29200,.tier2);//Gscarf            
	Add(3,5377,1,0,0,29200,.tier3);//GPipe             
	Add(3,5594,1,0,0,29200,.tier1);//Donut In Mouth    
	Add(3,5362,1,0,0,29200,.tier1);//Ninja Scroll      
	Add(3,5548,1,0,0,29200,.tier1);//Scarlet Rose      

    //CONSUMABLES
    Add(4,12221,1,0,0,29200,.tier);//Megaphone                
    Add(4,14533,1,0,0,29200,.tier);//Field Manual 100%        
    Add(4,14592,1,0,0,29200,.tier);//JOB Battle Manual        
    Add(4,12214,1,0,0,29200,.tier);//Convex Mirror            
    Add(4,13583,1,0,0,29200,.tier);//Convex Mirror 30 Box     
    Add(4,7621 ,1,0,0,29200,.tier);//Token Of Siegfried       
    Add(4,13601,1,0,0,29200,.tier);//Token of Siegfried 20 Box  
    Add(4,12210,1,0,0,29200,.tier);//Bubble Gum          

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "coinshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "coinshop"+.@i,1;
	npcshopattach "coinshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Coin Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+ getitemname(.@q[0]) +"^000000", ((.@preview && !@qe[7])?" ~ Preview...": ""), " ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Coin Shop]";
				mes "You're missing one or more requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Coin Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			
			getitem .@q[0],.@q[2];
			
			if (.Announce) announce strcharinfo(0)+" has bought a "+getitemname(.@q[0])+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "coinshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	coinshop	{
	
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
		
	mes "[RebelRO Coin Shop]";
	doevent "shop_coin::OnMenu";
	
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	coinshop1	FAKE_NPC,909:-1
-	shop	coinshop2	FAKE_NPC,909:-1
-	shop	coinshop3	FAKE_NPC,909:-1
-	shop	coinshop4	FAKE_NPC,909:-1
-	shop	coinshop5	FAKE_NPC,909:-1
